title SHELL CASE
author arrogant.gamer
homepage www.arrogantgamer.com

========
OBJECTS
========

( Floor Colour: #443333 )
( Lowered Wall: #888888 )

Crate
grey
.....
..0..
.000.
..0..
.....

Target
lightblue
00000
00.00
0...0
00.00
00000

Juxtapose
green red lightgreen yellow darkred
34113
10001
10204
10001
31143

Sokoban
green blue lightgreen yellow darkblue
31413
10001
10201
40001
31113

A_Frame
darkgrey red darkred
02.10
1...1
.....
1...1
01.20

B_Frame
darkgrey blue darkblue
01.10
1...1
.....
2...1
01.10

( EW -- east west )
( NS -- east west )

WhiteTile
#EEEEEE
00000
00000
00000
00000
00000

BlackTile
#550044 #770066
00001
00110
00001
01100
10001

collected
lightgreen green
.....
.111.
.101.
.111.
.....

5
lightgreen green
....1
.11..
1001.
.1.1.
...1.

8
lightgreen green
.101.
..11.
.1.1.
.....
.....

4
lightgreen green
.....
1....
01...
011..
1....

6
lightgreen green
.....
....1
..110
.1.10
....1

2
lightgreen green
.....
.....
..1..
.101.
1.011

7
lightgreen green
011.1
1.1..
11...
.....
.....

9
lightgreen green
.1.10
..110
...11
.1...
.....

3
lightgreen green
.....
.....
1..1.
.1101
..110

1
lightgreen green
.....
.1...
1....
111..
0111.

Player
white
.....
.....
.....
.....

=======
LEGEND
=======

( basic stuff)
p = Player and BlackTile
Background = WhiteTile
o = collected (the player starts here)

# = BlackTile
. = WhiteTile

( frames )
A = A_Frame
@ = A_Frame and BlackTile

B = B_Frame
& = B_Frame and BlackTile

k = crate
c = crate and BlackTile

t = target
x = target and BlackTile

( useful aggregates )
+ = collected and A_frame and BlackTile

( types )
Frame = A_Frame or B_Frame
FrameRider = Juxtapose or Sokoban
ooze = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or collected
Mover = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or Sokoban
FluxMover = Juxtapose
Barrier = BlackTile

=======
SOUNDS
=======

sfx1 96691504

================
COLLISIONLAYERS
================

WhiteTile
target
5, 4, 6, 8, 2, 7, 9, 3, 1
collected, player
Frame, FrameRider, crate
BlackTile

======
RULES
======

( movement for frame riders )
[> player] [ FrameRider ] -> [ player ] [> FrameRider]


( movement of the goo )
[> player] [ collected ]  -> [ player ] [> collected ]

[> Player] [5] -> [Player] [> 5]
[> Player] [8] -> [Player] [> 8]
[> Player] [2] -> [Player] [> 2]
[> Player] [4] -> [Player] [> 4]
[> Player] [6] -> [Player] [> 6]
[> Player] [7] -> [Player] [> 7]
[> Player] [9] -> [Player] [> 9]
[> Player] [1] -> [Player] [> 1]
[> Player] [3] -> [Player] [> 3]

( center to edges )
UP    [> collected] -> [8]
DOWN  [> collected] -> [2]
LEFT  [> collected] -> [4]
RIGHT [> collected] -> [6]

UP    [> 5] -> [8]
DOWN  [> 5] -> [2]
LEFT  [> 5] -> [4]
RIGHT [> 5] -> [6]

( edges to center ) 
DOWN  [> 8] -> [5]
UP    [> 2] -> [5]
LEFT  [> 6] -> [5]
RIGHT [> 4] -> [5]

( getting the goo into the frame feels goooood )
late [5 frame] -> [ collected frame ]

( center to corners )
LEFT  [> 8] -> [7]
RIGHT [> 8] -> [9]

LEFT  [> 2] -> [1]
RIGHT [> 2] -> [3]

UP    [> 4] -> [7]
DOWN  [> 4] -> [1]

UP    [> 6] -> [9]
DOWN  [> 6] -> [3]

( corners to center )
LEFT  [> 9] -> [8]
RIGHT [> 7] -> [8]

LEFT  [> 3] -> [2]
RIGHT [> 1] -> [2]

UP    [> 1] -> [4]
DOWN  [> 7] -> [4]

UP    [> 3] -> [6]
DOWN  [> 9] -> [6]

( Movers become can't move through or out of the flux )
[> Mover | Barrier ] -> [ Mover | Barrier ]
[> Mover Barrier ] -> [ Mover Barrier ]

( FluxFrame can move into the flux or out of it, but can only move in flux )
[> FluxMover Barrier | Barrier ] -> [ FluxMover Barrier | Barrier ] sfx1

( movers can't move through crates )
[> ooze | crate ] -> [ ooze | crate ]

(exit from edges )
DOWN  [> 2| no Barrier ] -> [|8 ]
UP    [> 8| no Barrier ] -> [|2 ]
LEFT  [> 4| no Barrier ] -> [|6 ]
RIGHT [> 6| no Barrier ] -> [|4 ]

(exit from corners )
UP    [> 7| no Barrier ] -> [|1]
LEFT  [> 7| no Barrier ] -> [|9]

UP    [> 9| no Barrier ] -> [|3]
RIGHT [> 9| no Barrier ] -> [|7]

DOWN  [> 3| no Barrier ] -> [|9]
RIGHT [> 3| no Barrier ] -> [|1]

DOWN  [> 1| no Barrier ] -> [|7]
LEFT  [> 1| no Barrier ] -> [|3]

( mounting and dismounting )
[action player] [ ooze a_frame ]  -> [player] [ action juxtapose ]
[action player] [ ooze b_frame ]  -> [player] [ action Sokoban ]

[action player] [ juxtapose ]   -> [player] [ action collected action a_frame ]
[action player] [ Sokoban ]   -> [player] [ action collected action b_frame ]

( mount/dismount actions )

( upon mount or dismount juxtapose flips the flux )
[ action juxtapose ] [ WhiteTile ] -> [ action juxtapose ] [ action 5 ]
[ action juxtapose ] [ BlackTile ] -> [ action juxtapose ] [ action 6 ]
[ action juxtapose ] -> [ juxtapose ]

[ action collected action a_frame ] [ WhiteTile ] -> [ action collected action a_frame ] [ action 5 ]
[ action collected action a_frame ] [ BlackTile ] -> [ action collected action a_frame ] [ action 6 ]

[ action 5 ] -> [ BlackTile ]
[ action 6 ] -> [ WhiteTile ]

[ action collected action a_frame ] -> [ collected a_frame ]


( frames can push crates )
[ > frameRider | frame ] -> [ > frameRider | > frame ]
[ > frameRider | crate ] -> [ > frameRider | > crate ]

==============
WINCONDITIONS
==============

all crate on target

