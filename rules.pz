title SHELL CASE
author arrogant.gamer
homepage www.arrogantgamer.com

========
OBJECTS
========

( Floor Colour: #443333 )
( Lowered Wall: #888888 )

Crate
grey
.....
..0..
.000.
..0..
.....

Target
lightblue
00000
00.00
0...0
00.00
00000

FluxFrame
green lightgreen black
2.2.2
.000.
20102
.000.
2.2.2

EarthFrame (pusher)
green lightgreen black
.222.
20002
20102
20002
.222.


AirFrame
green lightgreen black
.2.2.
20002
.010.
20002
.2.2.

F_Frame
darkgrey red darkred
..2..
.....
2...2
.....
..2..

E_Frame
darkgrey blue darkblue
.222.
2...2
2...2
2...2
.222.

A_Frame
darkgrey red darkred
.2.2.
2...2
.....
2...2
.2.2.

( EW -- east west )
( NS -- east west )

WhiteTile
#EEEEEE
00000
00000
00000
00000
00000

BlackTile
#550044 #770066
00001
00110
00001
01100
10001

collected
lightgreen green
.....
.111.
.101.
.111.
.....

5
lightgreen green
....1
.11..
1001.
.1.1.
...1.

8
lightgreen green
.101.
..11.
.1.1.
.....
.....

4
lightgreen green
.....
1....
01...
011..
1....

6
lightgreen green
.....
....1
..110
.1.10
....1

2
lightgreen green
.....
.....
..1..
.101.
1.011

7
lightgreen green
011.1
1.1..
11...
.....
.....

9
lightgreen green
.1.10
..110
...11
.1...
.....

3
lightgreen green
.....
.....
1..1.
.1101
..110

1
lightgreen green
.....
.1...
1....
111..
0111.

Player
white
.....
.....
.....
.....

=======
LEGEND
=======

( basic stuff)
p = Player and BlackTile
Background = WhiteTile
o = collected (the player starts here)

# = BlackTile
. = WhiteTile

( frames )
F = F_Frame
~ = F_Frame and BlackTile

E = E_Frame
& = E_Frame and BlackTile

A = A_Frame
@ = A_Frame and BlackTile

( objects )

k = crate
c = crate and BlackTile

t = target
x = target and BlackTile

( useful aggregates )
+ = collected and F_Frame and BlackTile

( types )
Frame = F_Frame or E_Frame or A_Frame
FrameRider = FluxFrame or EarthFrame or AirFrame
ooze = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or collected
FirmMover = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or EarthFrame or AirFrame
FluxMover = FluxFrame
Barrier = BlackTile
object = crate or frame

=======
SOUNDS
=======

sfx1 96691504

================
COLLISIONLAYERS
================

WhiteTile
target
5, 4, 6, 8, 2, 7, 9, 3, 1
collected, player
Frame, FrameRider, crate
BlackTile

======
RULES
======

( movement for frame riders )
[> player] [ FrameRider ] -> [ player ] [> FrameRider]


( movement of the goo )
[> player] [ collected ]  -> [ player ] [> collected ]

[> Player] [5] -> [Player] [> 5]
[> Player] [8] -> [Player] [> 8]
[> Player] [2] -> [Player] [> 2]
[> Player] [4] -> [Player] [> 4]
[> Player] [6] -> [Player] [> 6]
[> Player] [7] -> [Player] [> 7]
[> Player] [9] -> [Player] [> 9]
[> Player] [1] -> [Player] [> 1]
[> Player] [3] -> [Player] [> 3]

( center to edges )
UP    [> collected] -> [8]
DOWN  [> collected] -> [2]
LEFT  [> collected] -> [4]
RIGHT [> collected] -> [6]

UP    [> 5] -> [8]
DOWN  [> 5] -> [2]
LEFT  [> 5] -> [4]
RIGHT [> 5] -> [6]

( edges to center ) 
DOWN  [> 8] -> [5]
UP    [> 2] -> [5]
LEFT  [> 6] -> [5]
RIGHT [> 4] -> [5]

( getting the goo into the frame feels goooood )
late [5 frame] -> [ collected frame ]

( center to corners )
LEFT  [> 8] -> [7]
RIGHT [> 8] -> [9]

LEFT  [> 2] -> [1]
RIGHT [> 2] -> [3]

UP    [> 4] -> [7]
DOWN  [> 4] -> [1]

UP    [> 6] -> [9]
DOWN  [> 6] -> [3]

( corners to center )
LEFT  [> 9] -> [8]
RIGHT [> 7] -> [8]

LEFT  [> 3] -> [2]
RIGHT [> 1] -> [2]

UP    [> 1] -> [4]
DOWN  [> 7] -> [4]

UP    [> 3] -> [6]
DOWN  [> 9] -> [6]

( FirmMovers can't move through or out of the flux )
[> FirmMover | Barrier ] -> [ FirmMover | Barrier ]
[> FirmMover Barrier ] -> [ FirmMover Barrier ]

( FluxFrame can move into the flux or out of it, but can only move freely in the flux )
[> FluxMover Barrier | Barrier ] -> [ FluxMover Barrier | Barrier ] sfx1

( ooze can't push crates )
[> ooze | crate ] -> [ ooze | crate ]

(exit from edges )
DOWN  [> 2| no Barrier ] -> [|8 ]
UP    [> 8| no Barrier ] -> [|2 ]
LEFT  [> 4| no Barrier ] -> [|6 ]
RIGHT [> 6| no Barrier ] -> [|4 ]

(exit from corners )
UP    [> 7| no Barrier ] -> [|1]
LEFT  [> 7| no Barrier ] -> [|9]

UP    [> 9| no Barrier ] -> [|3]
RIGHT [> 9| no Barrier ] -> [|7]

DOWN  [> 3| no Barrier ] -> [|9]
RIGHT [> 3| no Barrier ] -> [|1]

DOWN  [> 1| no Barrier ] -> [|7]
LEFT  [> 1| no Barrier ] -> [|3]

( mounting and dismounting )
[action player] [ ooze F_Frame ]  -> [player] [ action FluxFrame  ]
[action player] [ ooze E_Frame ]  -> [player] [ action EarthFrame ]
[action player] [ ooze A_Frame ]  -> [player] [ action AirFrame   ]

[action player] [ FluxFrame  ]    -> [player] [ action collected action F_Frame ]
[action player] [ EarthFrame ]    -> [player] [ action collected action E_Frame ]
[action player] [ AirFrame   ]    -> [player] [ action collected action A_Frame ]

( mount/dismount actions )

( upon mount or dismount juxtapose flips the flux )

( on mount, mark flux for flip ) 
[ action FluxFrame ] [ WhiteTile ] -> [ action FluxFrame ] [ action 5 ]
[ action FluxFrame ] [ BlackTile ] -> [ action FluxFrame ] [ action 6 ]
[ action FluxFrame ] -> [ FluxFrame ]

( on dismount, mark flux for flip ) 
[ action collected action F_Frame ] [ WhiteTile ] -> [ action collected action F_Frame ] [ action 5 ]
[ action collected action F_Frame ] [ BlackTile ] -> [ action collected action F_Frame ] [ action 6 ]

[ action 5 ] -> [ BlackTile ]
[ action 6 ] -> [ WhiteTile ]

[ action collected action F_frame ] -> [ collected F_frame ]

( frames can push crates )

( FluxFrame can push crates in the flux, or into/out of the flux )
( EarthFrames can push crates while not in the flux )
[ > FluxFrame  | object ]             -> [ > FluxFrame | > object ]
[ > EarthFrame | > object | Barrier ] -> [ EarthFrame | object | Barrier ]

( AirFrame move in mysterious ways )
[ ^ AirFrame | object ] -> [ ^ AirFrame | < object ]
[ v AirFrame | object ] -> [ v AirFrame | < object ]

==============
WINCONDITIONS
==============

all crate on target

